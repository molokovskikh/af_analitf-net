import System
import System.Reflection
import System.IO
import System.Net.Mail
import System.Xml
import System.Xml.Linq
import System.Linq.Enumerable
import System.Xml.XPath.Extensions
import Mono.Cecil

def FileElement(file as string, id as string, element as XElement):
	return XElement(element.Name.Namespace + "File", XAttribute("Id", id), XAttribute("Source", Path.GetFullPath(file)))

def AddDirectory(id as string, path as string, document as XDocument, manager as XmlNamespaceManager):
	element = document.XPathSelectElement("/prefix:Wix/prefix:Fragment/prefix:DirectoryRef[@Id='$id']/prefix:Component", manager)
	for file in Directory.GetFiles(path):
		fileId = Path.GetFileName(file) + "_" + id
		element.Add(FileElement(file, fileId, element))

	for dir in Directory.GetDirectories(path):
		id = Path.GetFileName(dir)
		AddDirectory(id, dir, document, manager)

Global(
	Project: "AnalitF.Net.Service",
	HumanReadableName: "AnalitF.Net",
	Profile: @catalog,
	Variables : {
		@Production : {
			@UpdateDeployRoot: """\\acdcserv\WebApps\var\update"""
		},
		@Test : {
			@DeployRoot: """\\acdcserv\TEST""",
			@DeployAlias: "AnalitF.Net.Service.Test",
			@UpdateDeployRoot: """\\acdcserv\TEST\var-test\update"""
		}
	},
	JenkinsProject: "AnalitF.Net"
)

task @default, [@Build]

task @Build, [@GenerateAssemblyInfo, @BuildWebSite]

task @DeployApp, [@WebDeploy]

task "deploy:service", [@DeployPipeline]

task @deploy, ["Production", "git:tag", "deploy:service", "deploy:client", "deploy:update"]

task "deploy:client", ["deploy:client:package"]

task "deploy:client:package", ["build:client:package"]:
	impersonate = Configuration.Maybe.impersonate != null
	if impersonate:
		ImpersonateUser("deployer", '$sdfsd887!'):
			Cp("output/package/*.nupkg", "//offdc/MMedia/packages", true)
	else:
		Cp("output/package/*.nupkg", "//offdc/MMedia/packages", true)

task "build:client:package", ["build:client"]:
	bin = "output\\AnalitF.Net.Client\\AnalitF.Net.Client.exe"
	assembly = AssemblyDefinition.ReadAssembly(bin)
	version = assembly.Name.Version.ToString()
	build = Configuration.Maybe.buildId
	if build:
		version += "-build" + build
	RmDir("output/package", true)
	MkDir("output/package")
	MkDir("output/package/tools")
	Cp("output/setup/setup.exe", "output/package/tools/analitf.net.setup.exe", true)
	Cp(FileSet("**.*", BaseDirectory: "output/analitf.net.client/"), "output/package/tools/", true)
	spec = "output/package/analitf.net.nuspec"
	Cp("assets/package.nuspec", spec)
	specDoc = XDocument.Load(spec)
	versionNode = specDoc.Descendants().First({n| n.Name.LocalName == "version"})
	versionNode.Value = version
	specDoc.Save(spec)
	Exec("cmd.exe", "/C chocolatey.bat pack", BaseDirectory: "output/package").Execute()

task "deploy:update", ["build:client"]:
	dir = Globals.UpdateDeployRoot
	Rm("$dir/*")
	impersonate = Configuration.Maybe.impersonate != null
	if impersonate:
		ImpersonateUser("deployer", '$sdfsd887!'):
			Cp("output/Updater/*", dir, true)
	else:
		Cp("output/Updater/*", dir, true)

task "build:client", ["Production", "before:build:client", @BuildApp, "after:build:app", "build:setup", "build:update", "after:build:client"]

task "after:build:app":
	output = "output/AnalitF.Net.Client"
	Cp(FileSet("**", BaseDirectory: "lib/libmysqld"), output)
	Rm(FileSet("$output/*.xml"))
	print Globals.Environment
	if Globals.Environment == @Production:
		Rm(FileSet("$output/*.pdb"))

task "before:build:client":
	Globals.Project = "AnalitF.Net.Client"

task "after:build:client":
	Globals.Project = "AnalitF.Net.Service"

task "build:setup":
	Bash("./scripts/wix.sh")

task "setup:preprocess":
	bin = "output\\AnalitF.Net.Client\\AnalitF.Net.Client.exe"
	assembly = AssemblyDefinition.ReadAssembly(bin)
	File.WriteAllText("output/setup/version.txt", assembly.Name.Version.ToString())
	name = "output/setup/files.wxs"
	document = XDocument.Load(name)
	manager = XmlNamespaceManager(NameTable())
	manager.AddNamespace("prefix", "http://schemas.microsoft.com/wix/2006/wi")

	path = "output/AnalitF.Net.Client/"
	AddDirectory("INSTALLFOLDER", path, document, manager)
	document.Save(name)

task "before:build:update":
	Engine.Tasks.First({t| t.Name == @BuildApp}).Executed = false
	Globals.Project = "Updater"

task "build:update", ["before:build:update", @BuildApp]:
	Cp("output/setup/version.txt", "output/Updater/", true)
	Cp("output/AnalitF.Net.Client/*", "output/Updater/", true)
	Rm(FileSet("output/Updater/*.xml"))
	if Globals.Environment == @Production:
		Rm(FileSet("output/Updater/*.pdb"))

task "git:tag":
	return unless Globals.Environment == @production
	kind = Configuration.Maybe.kind
	Bash("./scripts/tag-release.sh $kind")
